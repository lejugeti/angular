load("//tools:defaults.bzl", "jasmine_node_test", "karma_web_test_suite", "ng_module", "nodejs_binary", "npm_package_bin", "ts_library")

package(default_visibility = ["//visibility:private"])

TEST_FILE = "signal_inputs_spec"

TEST_DEPS = [
    "//packages/core",
    "//packages/core/testing",
]

ts_library(
    name = "test_compiler_lib",
    testonly = True,
    srcs = ["signal_inputs_test_compiler.ts"],
    deps = [
        "//packages/compiler-cli",
        "//packages/compiler-cli/src/ngtsc/partial_evaluator",
        "//packages/compiler-cli/src/ngtsc/reflection",
        "//packages/compiler-cli/src/transformers/jit_transforms",
        "@npm//typescript",
    ],
)

nodejs_binary(
    name = "test_compiler",
    testonly = True,
    data = [":test_compiler_lib"],
    entry_point = ":signal_inputs_test_compiler.ts",
)

npm_package_bin(
    name = "processed_test_sources",
    testonly = True,
    outs = ["tansformed_%s.ts" % TEST_FILE],
    args = [
        "$(execpath %s.ts)" % TEST_FILE,
        "$@",
    ],
    data = ["%s.ts" % TEST_FILE],
    tool = ":test_compiler",
)

ts_library(
    name = "test_jit_lib",
    testonly = True,
    srcs = ["tansformed_%s.ts" % TEST_FILE],
    deps = TEST_DEPS,
)

ng_module(
    name = "test_lib",
    testonly = True,
    srcs = ["%s.ts" % TEST_FILE],
    deps = TEST_DEPS,
)

jasmine_node_test(
    name = "test",
    bootstrap = ["//tools/testing:node"],
    deps = [
        ":test_lib",
    ],
)

jasmine_node_test(
    name = "test_jit",
    bootstrap = ["//tools/testing:node"],
    deps = [
        ":test_jit_lib",
    ],
)

karma_web_test_suite(
    name = "test_web",
    deps = [
        ":test_lib",
    ],
)

karma_web_test_suite(
    name = "test_jit_web",
    deps = [
        ":test_jit_lib",
    ],
)
